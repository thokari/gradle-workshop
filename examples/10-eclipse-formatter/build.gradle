apply plugin: 'eclipse'

task projectSpecificFormatter {
    group 'IDE'
    description 'Adds a formatter definition to eclipse .settings folder'
    eclipseJdt.dependsOn it
    ext {
        formatterXml = file 'eclipse/formatter-java-conventions.xml'
        formatterName = ''
        formatterProperties = [:]
        propertiesTemplate = '''
            eclipse.preferences.version=1
            formatter_profile=_%%name%%
            formatter_settings_version=12
            org.eclipse.jdt.ui.exception.name=e
            org.eclipse.jdt.ui.gettersetter.use.is=true
            org.eclipse.jdt.ui.keywordthis=false
            org.eclipse.jdt.ui.overrideannotation=true
        '''.stripIndent()
    }
    doFirst {
        def profiles = new XmlParser().parse formatterXml
        formatterName = profiles.profile[0].attributes().name
        // Skip the properties that refer to Java source/target compatibility, as they
        // are configured by the eclipseJdt task.
        profiles.profile[0].setting.findAll {
            !('org.eclipse.jdt.core.compiler.source'.equals(it.attributes().id)) &&
            !('org.eclipse.jdt.core.compiler.compliance'.equals(it.attributes().id)) &&
            !('org.eclipse.jdt.core.compiler.codegen.targetPlatform'.equals(it.attributes().id))
        }.each {
            formatterProperties.put it.attributes().id, it.attributes().value
        }
        // this file is not generated by the eclipse plugin, so we need to create it
        file('.settings/org.eclipse.jdt.ui.prefs').text = propertiesTemplate.replaceAll '%%name%%', formatterName
    }
}

eclipse {
    jdt { // this refers to the file .settings/org.eclipse.jdt.core.prefs
        file {
            withProperties { it.putAll projectSpecificFormatter.formatterProperties }
        }
    }
}
